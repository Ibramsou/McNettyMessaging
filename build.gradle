buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'java-library'
}

allprojects {
    group 'fr.github.ibramsou'
    version '2.3.0'
}

apply plugin: 'java'

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    dependencies {
        api 'io.netty:netty-all:4.1.86.Final'
        api 'com.google.code.findbugs:annotations:3.0.1u2'
        api 'com.google.code.gson:gson:2.8.9'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    compileJava.options.encoding = 'UTF-8'

    if (subproject.name != 'test') {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        publishing {
            publications {
                jar(MavenPublication) {
                    from components.java
                    groupId = 'io.github.ibramsou'
                    artifactId = subproject.name
                    version = project.version

                    pom {
                        name = 'McNettyMessaging'
                        description = 'Mc Netty Messaging is a open-source Java Library used to interract between a Client <-> Server'
                        url = 'https://github.com/Ibramsou/McNettyMessaging'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'Bramsou'
                                name = 'Ibrahim'
                                email = 'braimsou@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git:github.com/Ibramsou/McNettyMessaging.git'
                            developerConnection = 'scm:git:ssh://github.com:Ibramsou/McNettyMessaging.git'
                            url = 'https://github.com/Ibramsou/McNettyMessaging'
                        }
                    }
                }
            }
            repositories {
                maven {
                    name = "OSSRH"
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }

        signing {
            sign publishing.publications.jar
        }
    }

}



